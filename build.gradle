buildscript {
    ext {
        compose_version = '_'
    }
    dependencies {
        classpath "io.nlopez.compose.rules:ktlint:_"
        classpath 'app.cash.paparazzi:paparazzi-gradle-plugin:_'
        classpath Square.wire.gradlePlugin
        classpath CashApp.licenseeGradlePlugin
    }
}

// Top-level build file where you can add configuration options common to all sub-projects/modules.
plugins {
    id 'com.android.application' apply false
    id 'com.android.library' apply false
    id 'org.jetbrains.kotlin.android' apply false
    id 'org.jmailen.kotlinter' apply false
    id 'de.mannodermaus.android-junit5' apply false
    id 'app.cash.licensee' apply false
    id 'app.cash.paparazzi' apply false
    id 'com.squareup.wire' apply false
}

tasks.register('clean') {
    delete rootProject.buildDir
}

if (hasProperty('buildScan')) {
    gradleEnterprise {
        buildScan {
            termsOfServiceUrl = "https://gradle.com/terms-of-service"
            termsOfServiceAgree = "yes"
        }
    }
}

subprojects {
    plugins.withId("app.cash.paparazzi") {
        // Defer until afterEvaluate so that testImplementation is created by Android plugin.
        afterEvaluate {
            dependencies.constraints {
                add("testImplementation", "com.google.guava:guava:_") {
                    attributes {
                        attribute(
                                TargetJvmEnvironment.TARGET_JVM_ENVIRONMENT_ATTRIBUTE,
                                objects.named(TargetJvmEnvironment, TargetJvmEnvironment.STANDARD_JVM)
                        )
                    }
                    because("LayoutLib and sdk-common depend on Guava's -jre published variant." +
                            "See https://github.com/cashapp/paparazzi/issues/906.")
                }
            }
        }
    }
}
